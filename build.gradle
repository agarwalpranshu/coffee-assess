plugins {
	id 'org.springframework.boot' version '2.7.3'
	id 'io.spring.dependency-management' version '1.0.13.RELEASE'
	id 'java'
}

group = 'com.elane'
version = '0.1'
sourceCompatibility = '11'
targetCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	mapstructVersion = '1.5.2.Final'
}

dependencies {
	
	// Spring Boot Dependencies
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	
	// JPA Static MetaModel Generator
	compileOnly 'org.hibernate:hibernate-jpamodelgen'
	annotationProcessor 'org.hibernate:hibernate-jpamodelgen'
	
	// Lombok for boilerplate code
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	// Devtools for instant changes
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	
	// MySQL DB connectivity
	runtimeOnly 'mysql:mysql-connector-java'
	
	// Mapstruct for Entity-DTO mappings
	compileOnly "org.mapstruct:mapstruct:$mapstructVersion"
	annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

sourceSets {
    main {
    	java {
    		srcDirs += "${buildDir}/gen/main/java"
    	}
    }
}

tasks.withType(JavaCompile) {
	options.annotationProcessorGeneratedSourcesDirectory = file("${buildDir}/gen/main/java")
}

tasks.named('test') {
	useJUnitPlatform()
}
